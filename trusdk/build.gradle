plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka-android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.72'
}
apply from: "ktlint.gradle"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 4
        versionName "0.2.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("String", "TRU_ID_DEVICE_ID_SERVER_URL",
                '"https://eu.api.tru.id/public/coverage/v0.1/device_ip"')
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
        // Emit warnings about not documented members.
        reportUndocumented = true
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId = 'id.tru.sdk'
            artifactId = 'tru-sdk-android'
            version = '0.2.4'
            artifact("$buildDir/outputs/aar/${project.name}-release.aar")
        }
    }
    repositories {
        maven {
            url "https://gitlab.com/api/v4/projects/22035475/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Deploy-Token"
                value = System.getenv("TRU_SDK_ANDROID_TOKEN")
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$app_compat_version"
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$ktx_version"
    //Networking
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    // Testing
    testImplementation 'org.robolectric:robolectric:4.5.1'
    testImplementation "junit:junit:${junit_version}"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okhttp_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2")
}
